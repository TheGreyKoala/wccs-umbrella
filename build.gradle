group = "de.koalaworks.wccs"
version "1.0.0"

buildscript {
    ext.wcmlVersion = "1.0.0"
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.avast.gradle:docker-compose-gradle-plugin:0.4.5"
    }
}

apply plugin: "base"
apply plugin: "distribution"
apply plugin: "docker-compose"

repositories {
    mavenLocal()
}

configurations {
    modules
    wcml
    other
}

dependencies {
    modules "de.koalaworks.wccs:wccs-annotation-service:1.0.0"
    modules "de.koalaworks.wccs:wccs-annotation-viewer:1.0.0"
    modules "de.koalaworks.wccs:wccs-classification-service:1.0.0"
    modules "de.koalaworks.wccs:wccs-classification-storage:1.0.0"
    modules "de.koalaworks.wccs:wccs-classification-storage-api:1.0.0"
    modules "de.koalaworks.wccs:wccs-web-app:1.0.0"
    wcml "de.koalaworks.wccs.wcml:de.koalaworks.wccs.wcml.distribution:$wcmlVersion@zip"

    other "de.koalaworks.wccs:wccs-annotator-plugin:1.0.0"
    other "de.koalaworks.wccs:wccs-wordpress-crawler:1.0.0"
}

task extractModules(type: Copy) {
    from {
        configurations.modules.collect { tarTree(it) }
    }
    into "$buildDir/modules"
}

task extractOther(type: Copy) {
    from {
        configurations.other.collect { tarTree(it) }
    }
    into "$buildDir/other"
}

task generateDockerComposeYaml(type: Copy) {
    from "docker-compose.template.yml"
    into "$buildDir"

    rename { file -> "docker-compose.yml" }
    expand(configurations.modules.dependencies.collectEntries {
        // For some reason "-" do not work in keys
        def moduleName = it.name.replace("-", "_")

        [
            (moduleName + "_root") : "./modules/" + it.name + "-" + it.version,
            (moduleName + "_version") : it.version
        ]
    })
}

distTar.dependsOn(generateDockerComposeYaml)
distZip.dependsOn(generateDockerComposeYaml)

distributions {
    main {
        baseName = "WebpageContentClassificationSystem"
        contents {
            from extractModules, extractOther, "$buildDir/docker-compose.yml"
            into ("wccs-web-content-modeling-language-$wcmlVersion") {
                from configurations.wcml
            }
        }
    }
}

composeUp.dependsOn(extractModules)
composeUp.dependsOn(generateDockerComposeYaml)

dockerCompose {
    removeContainers = true
    removeImages = "All"
    projectName = "wcts"
    dockerComposeWorkingDirectory = "./build"
}
