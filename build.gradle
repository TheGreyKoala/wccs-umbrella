version "1.0.0-SNAPSHOT"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.avast.gradle:docker-compose-gradle-plugin:0.4.5"
    }
}

apply plugin: "base"
apply plugin: "distribution"
apply plugin: "docker-compose"

repositories {
    mavenLocal()
}

configurations {
    modules
    other
}

dependencies {
    modules "de.koalaworks.wcts:wcts-annotation-viewer:0.0.1-SNAPSHOT"
    modules "de.koalaworks.wcts:wcts-storage:0.0.1-SNAPSHOT"
    modules "de.koalaworks.wcts:wcts-storage-api:0.0.1-SNAPSHOT"
    modules "de.koalaworks.wcts:wcts-typing-engine:0.0.1-SNAPSHOT"
    modules "de.koalaworks.wcts:wcts-web-app:0.0.1-SNAPSHOT"

    other "de.koalaworks.wcts:wcts-wordpress-crawler:0.0.1-SNAPSHOT"
}

task extractModules(type: Copy) {
    from {
        configurations.modules.collect { tarTree(it) }
    }
    into "$buildDir/modules"
}

task generateDockerComposeYaml(type: Copy) {
    from "docker-compose.template.yml"
    into "$buildDir"

    rename { file -> "docker-compose.yml" }
    expand(configurations.modules.dependencies.collectEntries {
        // For some reason "-" do not work in keys
        def moduleName = it.name.replace("-", "_")

        [
            (moduleName + "_root") : "./modules/" + it.name + "-" + it.version,
            (moduleName + "_version") : it.version
        ]
    })
}

distTar.dependsOn(generateDockerComposeYaml)
distZip.dependsOn(generateDockerComposeYaml)

distributions {
    main {
        contents {
            from extractModules, "$buildDir/docker-compose.yml"
            from configurations.other
        }
    }
}

composeUp.dependsOn(extractModules)
composeUp.dependsOn(generateDockerComposeYaml)

dockerCompose {
    removeContainers = true
    removeImages = "All"
    projectName = "wcts"
    dockerComposeWorkingDirectory = "./build"
}
